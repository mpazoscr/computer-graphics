#version 430

struct Complex
{
  float r;
  float i;
};

Complex add(Complex a, Complex b)
{
  return Complex(a.r + b.r, a.i + b.i);
}

Complex mult(Complex a, Complex b)
{
  return Complex(a.r * b.r - a.i * b.i, a.r * b.i + a.i * b.r);
}

Complex conjugate(Complex a)
{
  return Complex(a.r, -a.i);
}

Complex eulerExp(float s)
{
  return Complex(cos(s), sin(s));
}

layout (std430, binding = 0) readonly restrict buffer BufferH0
{
  Complex h0[];
};

layout (std430, binding = 1) restrict buffer BufferSpectrum
{
  Complex spectrum[];
};

layout (std430, binding = 2) writeonly restrict buffer BufferDispX
{
  Complex dispX[];
};

layout (std430, binding = 3) writeonly restrict buffer BufferDispZ
{
  Complex dispZ[];
};

layout (std430, binding = 4) writeonly restrict buffer BufferGradX
{
  Complex gradX[];
};

layout (std430, binding = 5) writeonly restrict buffer BufferGradZ
{
  Complex gradZ[];
};

uniform uvec2 meshSize;
uniform vec2 physicalSize;
uniform float g;
uniform float t;

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

void main()
{
  const float kPi = 3.141592653589793;
  const float epsilon = 1e-12f;

  uint indexX = gl_GlobalInvocationID.x;
  uint indexZ = gl_GlobalInvocationID.y;

  uint sizeX = meshSize.x;
  uint sizeZ = meshSize.y;

  float lengthX = physicalSize.x;
  float lengthZ = physicalSize.y;

  uint indexXMirrored = sizeX - indexX - 1;
  uint indexZMirrored = sizeZ - indexZ - 1;

  uint index = indexZ * sizeX + indexX;
  uint indexMirrored = indexZMirrored * sizeX + indexXMirrored;

  float kx = (indexX - sizeX / 2.0f) * (2.0f * kPi / lengthX);
  float kz = (indexZ - sizeZ / 2.0f) * (2.0f * kPi / lengthZ);
  float kLength = sqrt(kx * kx + kz * kz);
  float w = sqrt(g * kLength);

  spectrum[index] = add(mult(h0[index], eulerExp(w * t)),
                        mult(conjugate(h0[indexMirrored]), eulerExp(-w * t)));

  gradX[index] = Complex(-kx * spectrum[index].i, kx * spectrum[index].r);
  gradZ[index] = Complex(-kz * spectrum[index].i, kz * spectrum[index].r);

  if (kLength > epsilon)
  {
    dispX[index] = Complex(kx / kLength * spectrum[index].i, -kx / kLength * spectrum[index].r);
    dispZ[index] = Complex(kz / kLength * spectrum[index].i, -kz / kLength * spectrum[index].r);

    //jxx[index] = Complex(kx * kx / kLength * spectrum[index].r, kx * kx / kLength * spectrum[index].i);
    //jzz[index] = Complex(kz * kz / kLength * spectrum[index].r, kz * kz / kLength * spectrum[index].i);
    //jxz[index] = Complex(kx * kz / kLength * spectrum[index].r, kx * kz / kLength * spectrum[index].i);
  }
  else
  {
    dispX[index] = Complex(0.0f, 0.0f);
    dispZ[index] = Complex(0.0f, 0.0f);

    //jxx[index] = Complex(0.0f, 0.0f);
    //jzz[index] = Complex(0.0f, 0.0f);
    //jxz[index] = Complex(0.0f, 0.0f);
  }
}
