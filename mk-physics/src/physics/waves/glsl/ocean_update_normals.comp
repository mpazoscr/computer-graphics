#version 430

struct Complex
{
  float r;
  float i;
};

Complex add(Complex a, Complex b)
{
  return Complex(a.r + b.r, a.i + b.i);
}

Complex mult(Complex a, Complex b)
{
  return Complex(a.r * b.r - a.i * b.i, a.r * b.i + a.i * b.r);
}

Complex conjugate(Complex a)
{
  return Complex(a.r, -a.i);
}

Complex eulerExp(float s)
{
  return Complex(cos(s), sin(s));
}

struct VertexPN
{
  vec3 pos;
  vec3 normal;
};

layout (std430, binding = 0) readonly restrict buffer BufferH
{
  Complex data[];
} h;

layout (std430, binding = 1) readonly restrict buffer BufferGradX
{
  Complex data[];
} gradX;

layout (std430, binding = 2) readonly restrict buffer BufferGradZ
{
  Complex data[];
} gradZ;

layout (std430, binding = 3) writeonly restrict buffer BufferMesh
{
  VertexPN data[];
} mesh;

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

uniform uvec2 meshSize;

void main()
{
  uint indexX = gl_GlobalInvocationID.x;
  uint indexZ = gl_GlobalInvocationID.y;

  uint index = indexZ * meshSize.x + indexX;

  float sign = (((indexX + indexZ) & 0x01) != 0) ? 1.0f : -1.0f; // Reverse sign here, since we need a minus later anyway

  mesh.data[index].normal = normalize(vec3(sign * gradX.data[index].r, 1.0f, sign * gradZ.data[index].r));
}
