#version 430

struct Complex
{
  float r;
  float i;
};

Complex add(Complex a, Complex b)
{
  return Complex(a.r + b.r, a.i + b.i);
}

Complex mult(Complex a, Complex b)
{
  return Complex(a.r * b.r - a.i * b.i, a.r * b.i + a.i * b.r);
}

Complex conjugate(Complex a)
{
  return Complex(a.r, -a.i);
}

Complex eulerExp(float s)
{
  return Complex(cos(s), sin(s));
}

struct VertexPN
{
  float x, y, z;
  float nx, ny, nz;
};

layout (std430, binding = 0) readonly restrict buffer BufferH
{
  Complex h[];
};

layout (std430, binding = 1) readonly restrict buffer BufferDispX
{
  Complex dispX[];
};

layout (std430, binding = 2) readonly restrict buffer BufferDispZ
{
  Complex dispZ[];
};

layout (std430, binding = 3) writeonly restrict buffer BufferMesh
{
  VertexPN mesh[];
};

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

uniform uvec2 meshSize;
uniform float dispFactor;

void main()
{
  uint indexX = gl_GlobalInvocationID.x;
  uint indexZ = gl_GlobalInvocationID.y;

  uint sizeX = meshSize.x;
  uint sizeZ = meshSize.y;

  uint index = indexZ * sizeX + indexX;

  float sign = (((indexX + indexZ) & 0x01) != 0) ? -1.0f : 1.0f;

  mesh[index].x = (indexX - sizeX / 2.0f) + dispFactor * sign * dispX[index].r;
  mesh[index].y = sign * h[index].r;
  mesh[index].z = (indexZ - sizeZ / 2.0f) + dispFactor * sign * dispZ[index].r;
}
